apply plugin: 'com.android.application'

def gitCommits = 'git rev-list --all --count'.execute([], rootDir).text.trim().toInteger()

def timestamp = new Date().getTime()

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId "org.nv95.openmanga"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode gitCommits
        versionName "5.0"

		resConfigs "en", "ru", "tr", "uk"
        buildConfigField "boolean", "SELFUPDATE_ENABLED", "true"
        buildConfigField "String", "SELFUPDATE_URL", "\"http://anibreak.ru/v.0.3/get/openmanga/version\""
        buildConfigField "String", "SYNC_URL", "\"http://openmanga.pythonanywhere.com/api/v1\""
		buildConfigField "long", "TIMESTAMP", "${timestamp}L"
    }

    signingConfigs {
        debug {
            storeFile file("debug.jks")
            storePassword "develop"
            keyAlias "develop"
            keyPassword "develop"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
			setProperty("archivesBaseName", "OpenManga-v${defaultConfig.versionName}")
            resValue "string", "app_name", "OpenManga"
        }

		debug {
            zipAlignEnabled true
            applicationIdSuffix ".debug"
			versionNameSuffix="a"
			signingConfig signingConfigs.debug
            resValue "string", "app_name", "OpenManga Debug"
        }

        fdroid {
            initWith release
            buildConfigField "boolean", "SELFUPDATE_ENABLED", "false"
            versionNameSuffix="-fdroid"
        }
    }

	compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
    }
}

ext {
	supportLib = '28.0.0-rc02'
}

dependencies {
	implementation "com.android.support:design:${supportLib}"
	implementation "com.android.support:support-v4:${supportLib}"
	implementation "com.android.support:recyclerview-v7:${supportLib}"
	implementation "com.android.support:cardview-v7:${supportLib}"
    implementation "com.android.support:exifinterface:${supportLib}"

	implementation 'com.squareup.okhttp3:okhttp:3.11.0'
	implementation 'com.squareup.duktape:duktape-android:1.3.0'
	implementation 'info.guardianproject.netcipher:netcipher:2.0.0-beta1'
	implementation 'info.guardianproject.netcipher:netcipher-okhttp3:2.0.0-alpha1'
	implementation 'org.jsoup:jsoup:1.11.3'
	implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
	implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
}
